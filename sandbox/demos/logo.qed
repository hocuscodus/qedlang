bool Dialog(String title, String question) {
  var yesButton = new QedTextButton("Yes") -> return true
  var noButton = new QedTextButton("No") -> return false

  <out: rect; opacity: 30%;>
  <| out: roundRect; fillStyle: "#D0D0D0"; radius: 10; align: 50%;
    <out: rect; fillStyle: "#0090C0";
      <|
        <size: 10;>
        <out: title; fillStyle: "white";
         align: 50%;>
        <size: 5;>
      >
    >
    <\ fillStyle: "black"; fontSize: 17;
      <size: [30, 20];>
      <out: question; align: 50%; >
      <size: 30;>
    >
    <_ align: 100%; pos: -10; radius: 5;
      <out: yesButton; fillStyle: "#40C040";>
      <size: 10;>
      <out: noButton; fillStyle: "#C04040";>
    >
  >
}

bool isOn = false
float angle = 0
float speed = 0
float acc

void Animation() {
  float start
  float delta = 0

  speed = acc
  start = QedAnimation()

  while (speed > 0) {
    angle += speed * delta / 400000 * 2 * 3.14159265
    speed += acc

    if (speed > 500)
      speed = 500

    delta = QedAnimation() - start
    start += delta
  }

  speed = 0
}
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                               
QedImage image = QedImageLoader("biglogo.png")
QedTextButton button

button = new QedTextButton("  Turn on  ").textStyle("#7f6921") -> {
  if (!isOn || Dialog("Turn fan off", "Are you sure?")) {
    isOn = !isOn
    acc = isOn ? 2 : -2
    button.text = isOn ? "  Turn off  " : "  Turn on  "

    if (speed == 0)
      new Animation()
  }
}

<rotation: angle * 2 * 3.14159265; size: 700;
  <out: image; align: 50%;>
>
<out: button; align: 1; pos: -25; fillStyle: "#d4af37";>
